#include <WiFi.h>
#include <HTTPClient.h>
#include <WebServer.h>
#include <ArduinoJson.h>
#include "esp_camera.h"

const char* ssid     = "S24";
const char* password = "segundolugar";
const char* PC_IP    = "10.12.13.90";

#define LED_PIN 2

// Pinos câmara
#define PWDN_GPIO_NUM  -1
#define RESET_GPIO_NUM -1
#define XCLK_GPIO_NUM  10
#define SIOD_GPIO_NUM  40
#define SIOC_GPIO_NUM  39
#define Y9_GPIO_NUM    48
#define Y8_GPIO_NUM    11
#define Y7_GPIO_NUM    12
#define Y6_GPIO_NUM    14
#define Y5_GPIO_NUM    16
#define Y4_GPIO_NUM    18
#define Y3_GPIO_NUM    17
#define Y2_GPIO_NUM    15
#define VSYNC_GPIO_NUM 38
#define HREF_GPIO_NUM  47
#define PCLK_GPIO_NUM  13

WebServer server(80);

// Handlers
void handleImage() {
  camera_fb_t* fb = esp_camera_fb_get();
  if (!fb) { server.send(500, "text/plain", "Camera error"); return; }
  server.sendHeader("Content-Type", "image/jpeg");
  server.sendHeader("Content-Length", String(fb->len));
  server.send_P(200, "image/jpeg", (const char*)fb->buf, fb->len);
  esp_camera_fb_return(fb);
}

void handleLedOn() {
  digitalWrite(LED_PIN, HIGH);
  server.send(200, "text/plain", "LED ON");
}

void handleLedOff() {
  digitalWrite(LED_PIN, LOW);
  server.send(200, "text/plain", "LED OFF");
}

void initCamera() {
  camera_config_t config;
  config.ledc_channel = LEDC_CHANNEL_0;
  config.ledc_timer   = LEDC_TIMER_0;
  config.pin_d0       = Y2_GPIO_NUM;
  config.pin_d1       = Y3_GPIO_NUM;
  config.pin_d2       = Y4_GPIO_NUM;
  config.pin_d3       = Y5_GPIO_NUM;
  config.pin_d4       = Y6_GPIO_NUM;
  config.pin_d5       = Y7_GPIO_NUM;
  config.pin_d6       = Y8_GPIO_NUM;
  config.pin_d7       = Y9_GPIO_NUM;
  config.pin_xclk     = XCLK_GPIO_NUM;
  config.pin_pclk     = PCLK_GPIO_NUM;
  config.pin_vsync    = VSYNC_GPIO_NUM;
  config.pin_href     = HREF_GPIO_NUM;
  config.pin_sscb_sda = SIOD_GPIO_NUM;
  config.pin_sscb_scl = SIOC_GPIO_NUM;
  config.pin_pwdn     = PWDN_GPIO_NUM;
  config.pin_reset    = RESET_GPIO_NUM;
  config.xclk_freq_hz = 20000000;
  config.pixel_format = PIXFORMAT_JPEG;
  config.frame_size   = FRAMESIZE_VGA;  // 640x480
  config.jpeg_quality = 12;
  config.fb_count     = 1;

  esp_err_t err = esp_camera_init(&config);
  if (err != ESP_OK) {
    Serial.printf("Camera init failed: 0x%x\n", err);
  } else {
    Serial.println("Camera OK");
  }
}


void setup() {
  Serial.begin(115200);
  pinMode(LED_PIN, OUTPUT);
  digitalWrite(LED_PIN, LOW);

  WiFi.begin(ssid, password);
  Serial.print("Conectando WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("\nIP: " + WiFi.localIP().toString());

  initCamera();

  server.on("/image",   handleImage);
  server.on("/led/on",  handleLedOn);
  server.on("/led/off", handleLedOff);
  server.begin();
  Serial.println("WebServer iniciado");
}


void loop() {
  server.handleClient(); // manter o WebServer ativo

  static unsigned long lastDetection = 0;
  if (millis() - lastDetection < 1000) return;
  lastDetection = millis();

  String imageUrl = "http://" + WiFi.localIP().toString() + "/image";
  String body = "{\"api_key\":\"Kno6UrEbmrpiEYq3SLBW\","
                "\"inputs\":{\"image\":{\"type\":\"url\","
                "\"value\":\"" + imageUrl + "\"}}}";

  HTTPClient http;
  http.begin(String("http://") + PC_IP +
             ":9001/snowies-workspace/workflows/bike-vehicle-detection-logger-1779314546977");
  http.addHeader("Content-Type", "application/json");

  int code = http.POST(body);

  if (code == 200) {
    String response = http.getString();
    Serial.println(response);

    
    DynamicJsonDocument doc(4096);
    deserializeJson(doc, response);

    bool carDetected = false;
    JsonArray predictions = doc[0]["outputs"]["predictions"]["predictions"];
    for (JsonObject pred : predictions) {
      String cls = pred["class"].as<String>();
      if (cls == "Car") {
        carDetected = true;
        break;
      }
    }

    
    if (carDetected) {
      Serial.println("CAR DETECTED — LED ON");
      digitalWrite(LED_PIN, HIGH);
      delay(300);
      digitalWrite(LED_PIN, LOW);
    }

  } else {
    Serial.println("Error HTTP: " + String(code));
  }

  http.end();
}